apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

repositories {
    mavenCentral()
    maven {
        name = "bukkit-central"
        url = "http://repo.bukkit.org/content/groups/public/"
    }
	maven {
        name = "plugin-metrics"
        url = "http://repo.mcstats.org/content/repositories/public"
    }
}

compileJava {
	sourceCompatibility '1.6'
	targetCompatibility '1.6'
}

dependencies {
	compile 'org.bukkit:bukkit:1.7.2-R0.4-SNAPSHOT'
	compile 'org.mcstats.bukkit:metrics-lite:R6'
}


//buildNumber is deprecated
def buildnumber = 0
if(System.getenv().TRAVIS_BUILD_NUMBER != null)
{
	buildnumber = System.getenv().TRAVIS_BUILD_NUMBER
}

// version and stuff
version = "2.0.0.${buildnumber}"
ext.minecraftVersion = "1.7.2"
group = "com.jadarstudios.rankcapes.bukkit"
archivesBaseName = "RankCapes-bukkit"

// jar naming
jar
{
    appendix = "${project.ext.minecraftVersion}"
}

// fix mcmod.info and include resources
processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.ext.minecraftVersion
    }
}

uploadArchives
{
	repositories.mavenDeployer
	{
		repository(url: "http://repo.jadarstudios.com/content/repositories/public/")
		{
			authentication(userName: System.getenv().MVN_USER, password: System.getenv().MVN_PASS)
		}
		
		pom.project
		{
			licenses
			{
				license
				{
					name 'The MIT License'
					url 'https://raw2.github.com/jadar/RankCapes/master/LICENSE'
					distribution 'repo'
				}
			}
			
			developers
			{
				developer
				{
					id 'jadar'
					name 'Jadar'
					roles { role 'developer' }
				}
			}
			
			scm 
			{
                url 'https://github.com/jadar/RankCapes/tree/bukkit'
                connection 'scm:git:git://github.com/jadar/RankCapes.git'
                developerConnection 'scm:git:git@github.com:jadar/RankCapes.git'
            }

            issueManagement 
            {
                system 'github'
                url 'https://github.com/jadar/RankCapes/issues'
            }
            
			artifactId = project.archivesBaseName
			name = project.archivesBaseName
			packaging = 'jar'
			version = "${project.ext.minecraftVersion}-${project.version}"
			description 'RankCapes is a Bukkit plugin and Minecraft client mod combo that allows server owners to give their players custom capes based on permissions.'
		}
	}
}